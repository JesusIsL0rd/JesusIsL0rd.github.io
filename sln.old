@echo off
:: SearchLibNet
rmdir /Q /S dl
cls

::Downloads/Creates the library files
powershell -Command "(New-Object Net.WebClient).DownloadFile('https://jesusisl0rd.github.io/lib', 'lib.txt')"
powershell -Command "(New-Object Net.WebClient).DownloadFile('https://jesusisl0rd.github.io/libn', 'libn.txt')"

:: Sets the Book ID
echo SearchLibNet
echo.
if exist "found.txt" ( echo -- Previously found book results exist, new ones will be added to the end --
echo if you would prefer to start a fresh list, delete found.txt and run this again
echo.
)
echo Book ID -^> Open OverDrive.com. Search, Select book, ID is number after /media/
echo Manually type ID into the box below or right click to paste
echo.
set bkid=error
set /p bkid=Enter the book ID:
call set this=%%bkid
cls
echo Searching. This should take under a minute. Hang on.

::Starts the library downloads. Uses lib.txt as its source
md dl
setlocal
set filename=lib.txt
for /F "tokens=*" %%a in ('type %FileName%') do (
	powershell -Command "(New-Object Net.WebClient).DownloadFile('https://www.overdrive.com/_Ajax/get-libraries-for-media?mediaId=%bkid%&resultLimit=1&distance=1&position=%%a', 'dl\%%a.txt')"
)
endlocal

:: This is for libraries that have no GPS location
setlocal
set filename=libn.txt
for /F "tokens=*" %%q in ('type %FileName%') do (
	powershell -Command "(New-Object Net.WebClient).DownloadFile('https://www.overdrive.com/_Ajax/get-libraries-for-media?mediaId=%bkid%&resultLimit=1&q=%%q', 'dl\%%q.txt')"
)
endlocal


:: Delete Libraries with no results
for %%j in (dl\*) do if %%~zj lss 25 del "%%~j"
:: Cleans up the dl files for pretty output
powershell "gci 'dl' -Filter *.txt | ren -NewName { $_.Name -replace '^[^\#]*\#' }"
powershell "gci 'dl' -Filter *.txt | ren -NewName { $_.Name -replace '(.+?)_.+','$1' }"

:: If files exist. yes results. If not, no result
for /F %%i in ('dir /b "%cd%\dl\*.*"') do (
goto resultsfound
)
goto noresults

:: Echo's the found.txt file for easy viewing.
:resultsfound
echo -------- %bkid% -------- >> found.txt
for %%f in ("dl\*") do echo %%~nf >> found.txt
cls
echo These libraries have the book in question
echo see found.txt for an easy-to-copy list
echo.
type found.txt
echo.
call :cleanup
pause
exit

:: If no book is found
:noresults
call :cleanup
cls
echo Sorry, the book in question is not in any of my libraries...
echo.
pause
exit

:cleanup
rmdir /Q /S dl
del /S /Q lib.txt libn.txt >nul
